plugins {
    id "com.android.application"
    // START: FlutterFire Configuration
    id 'com.google.gms.google-services'
    // END: FlutterFire Configuration
    // START: FlutterFire Configuration
    // END: FlutterFire Configuration
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "ir.loqmeapp.application"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = "29.0.13846066"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    defaultConfig {
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdkVersion flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        // Add Google Sign-In configuration and application name
        manifestPlaceholders = [
            'GOOGLE_CLIENT_ID': '1033973813560-jum3tbbqu1oec1je2us6orv592eod98d.apps.googleusercontent.com',
            'applicationName': 'io.flutter.app.FlutterApplication'
        ]
    }

    flavorDimensions "market"

    productFlavors {
        iran {
            dimension "market"
            applicationId "ir.loqmeapp.application"
            resValue "string", "app_name", "لقمه"
        }
        global {
            dimension "market"
            applicationId "ir.loqmeapp.application"
            resValue "string", "app_name", "Slice"
        }
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            signingConfig keystorePropertiesFile.exists() ? signingConfigs.release : signingConfigs.debug
            minifyEnabled false
            shrinkResources false
        }
    }
}

flutter {
    source = "../.."
}

// Exclude billing libraries per flavor to prevent duplicate IInAppBillingService class conflicts
configurations.all { config ->
    if (config.name.toLowerCase().contains("global")) {
        config.exclude group: 'com.github.cafebazaar.Poolakey', module: 'poolakey'
    }
    if (config.name.toLowerCase().contains("iran")) {
        config.exclude group: 'com.revenuecat.purchases', module: 'purchases'
    }
}
